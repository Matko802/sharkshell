import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import Quickshell.Wayland

Scope {
  id: root

  property bool open: false
  property string query: ""
  property int selIdx: 0
  property int hoverIdx: -1
  property bool mouseOverList: false
  property string tab: "text"
  property int swipeIdx: 0
  property var allEntries: []
  property var thumbs: ({})
  readonly property string thumbDir: Quickshell.env("HOME") + "/.cache/quickshell/clipboard"
  readonly property string fontFamily: Theme.fontFamily
  readonly property color matchColor: Theme.match

  property bool closePending: false

  function requestClose() {
    if (!root.open || root.closePending)
      return
    root.closePending = true
    root.open = false
    closeTimer.restart()
  }

  function forceClose() {
    closeTimer.stop()
    root.closePending = false
    root.open = false
  }

  Timer {
    id: closeTimer
    interval: 200
    onTriggered: {
      root.closePending = false
    }
  }

  function toggle() {
    if (root.closePending) {
      closeTimer.stop()
      root.closePending = false
      root.open = false
    }
    if (root.open) {
      root.requestClose()
      return
    }
    closeTimer.stop()
    root.closePending = false
    root.query = ""
    root.selIdx = 0
    root.hoverIdx = -1
    root.open = true
    listProc.running = true
  }

  function preview(line) {
    const i = line.indexOf("\t")
    return i >= 0 ? line.slice(i + 1) : line
  }

  function esc(s) {
    return s.replace(/&/g, "&amp;").replace(/</g, "&lt;").replace(/>/g, "&gt;")
  }

  function hl(raw) {
    const p = root.preview(raw)
    const q = root.query.toLowerCase()
    if (q === "")
      return root.esc(p)
    const i = p.toLowerCase().indexOf(q)
    if (i < 0)
      return root.esc(p)
    return root.esc(p.slice(0, i)) + "<font color=\"" + root.matchColor + "\">" + root.esc(p.slice(i, i + q.length)) + "</font>" + root.esc(p.slice(i + q.length))
  }

  function shellEscape(s) {
    return "'" + s.replace(/'/g, "'\\''") + "'"
  }

  function idOf(line) {
    return line.slice(0, line.indexOf("\t"))
  }

  function isImage(line) {
    const p = root.preview(line)
    return p.startsWith("[[ binary data") && / (?:png|jpe?g|gif|bmp|webp)\b/i.test(p)
  }

  function paste(i) {
    const entry = root.currentList[i]
    if (!entry)
      return
    root.forceClose()
    pasteDelay.restart()
    pasteProc.command = ["sh", "-c", "printf '%s\\n' " + root.shellEscape(entry) + " | cliphist decode | wl-copy"]
  }

  function remove(i) {
    const entry = root.currentList[i]
    if (!entry)
      return
    delProc.command = ["sh", "-c", "printf '%s\\n' " + root.shellEscape(entry) + " | cliphist delete"]
    delProc.running = true
  }

  function switchTab() {
    root.tab = root.tab === "text" ? "images" : "text"
  }

  function pageStep() {
    const g = imgGrid
    if (!g)
      return 1
    const cols = Math.max(1, Math.floor(g.width / g.cellWidth))
    const rows = Math.max(1, Math.floor(g.height / g.cellHeight))
    return cols * rows
  }

  Timer {
    id: pasteDelay
    interval: 100
    onTriggered: pasteProc.running = true
  }

  Process {
    id: pasteProc
  }
  Process {
    id: delProc
    onExited: listProc.running = true
  }

  Process {
    id: listProc
    command: ["cliphist", "list"]
    stdout: StdioCollector {
      onStreamFinished: {
        root.allEntries = this.text.split("\n").filter(l => l.trim() !== "" && !root.isEmojiOnly(root.preview(l)))
        thumbProc.running = root.allEntries.some(l => root.isImage(l))
      }
    }
  }

  Process {
    id: thumbProc
    onStarted: root.thumbs = ({})
    command: ["stdbuf", "-oL", "sh", "-c",
      "mkdir -p \"$HOME/.cache/quickshell/clipboard\"\n"
      + "tmp=$(mktemp)\n"
      + "cliphist list | while IFS= read -r line; do\n"
      + "  prev=${line#*$'\\t'}\n"
      + "  case $prev in \"[[ binary data\"*) ;; *) continue ;; esac\n"
      + "  case $prev in *png*|*jpeg*|*jpg*|*gif*|*bmp*|*webp*) ;; *) continue ;; esac\n"
      + "  id=${line%%$'\\t'*}\n"
      + "  f=\"$HOME/.cache/quickshell/clipboard/$id.png\"\n"
      + "  if [ -f \"$f\" ]; then echo \"$id\"; continue; fi\n"
      + "  echo \"$line\" >> \"$tmp\"\n"
      + "done\n"
      + "xargs -P 6 -I {} sh -c 'id=$(printf \"%s\" \"{}\" | cut -f1); f=\"$HOME/.cache/quickshell/clipboard/$id.png\"; [ -f \"$f\" ] && { echo \"$id\"; exit 0; }; printf \"%s\\n\" \"{}\" | cliphist decode | magick - -resize \"256x256>\" \"$f\" 2>/dev/null && echo \"$id\"' < \"$tmp\"\n"
      + "rm -f \"$tmp\"\n"
    ]
    stdout: SplitParser {
      onRead: line => {
        const l = String(line).trim()
        if (l === "")
          return
        const t = Object.assign({}, root.thumbs)
        t[l] = true
        root.thumbs = t
      }
    }
  }

  function isEmojiOnly(s) {
    const t = s.replace(/\s+/g, "")
    if (t === "")
      return false
    const re = /^(?:[\u2600-\u27BF\u2B00-\u2BFF\u{1F000}-\u{1FAFF}\u{1F1E6}-\u{1F1FF}\uFE0F\u200D\u2122\u203C\u2049]|(?:[#*0-9]\uFE0F?\u20E3))+$/
    return re.test(t)
  }

  IpcHandler {
    target: "clipboard"
    function toggle() {
      root.toggle()
    }
    function close() {
      root.forceClose()
    }
  }

  readonly property var lcPreviews: root.allEntries.map(l => root.preview(l).toLowerCase())

  readonly property var results: {
    const q = root.query.toLowerCase()
    return root.allEntries.filter((l, i) => q === "" || root.lcPreviews[i].includes(q))
  }
  readonly property var textEntries: root.results.filter(l => !root.isImage(l))
  readonly property var imageEntries: root.results.filter(l => root.isImage(l))

  readonly property var currentList: root.tab === "images" ? root.imageEntries : root.textEntries

  onTabChanged: {
    root.selIdx = 0
    root.hoverIdx = -1
    root.swipeIdx = root.tab === "images" ? 1 : 0
  }

  onQueryChanged: {
    root.selIdx = 0
    root.hoverIdx = -1
  }
  onResultsChanged: {
    const cl = root.currentList || []
    if (root.selIdx >= cl.length)
      root.selIdx = Math.max(0, cl.length - 1)
    if (root.hoverIdx >= cl.length)
      root.hoverIdx = -1
  }
  onSelIdxChanged: {
    let g = null, top = 0, itemH = 0
    if (root.tab === "images") {
      g = imgGrid
      if (!g) return
      const cols = Math.max(1, Math.floor(g.width / g.cellWidth))
      top = Math.floor(root.selIdx / cols) * g.cellHeight
      itemH = g.cellHeight
    } else {
      g = list
      if (!g) return
      top = root.selIdx * 28
      itemH = 28
    }
    const visH = g.height
    if (visH <= 0) return
    let y = g.contentY
    if (top < y) y = top
    else if (top + itemH > y + visH) y = top + itemH - visH
    const maxY = Math.max(0, g.contentHeight - visH)
    g.contentY = Math.max(0, Math.min(maxY, y))
  }

  TextMetrics {
    id: tm
    font.family: root.fontFamily
    font.pointSize: 12
    text: "M"
  }
  readonly property int boxWidth: Math.round(60 * tm.advanceWidth) + 24
  readonly property int imgCellH: 96

  PanelWindow {
    id: panel
    anchors.top: true
    anchors.left: true
    anchors.right: true
    anchors.bottom: true
    exclusionMode: ExclusionMode.Ignore
    color: "transparent"
    WlrLayershell.namespace: "quickshell-modal"
    mask: Region {
      item: card
      Region { item: tabsPill }
    }
    WlrLayershell.layer: WlrLayer.Overlay
    WlrLayershell.keyboardFocus: WlrKeyboardFocus.Exclusive
    visible: root.open || root.closePending

    onVisibleChanged: { if (visible && root.open) search.forceActiveFocus() }

    Timer {
      running: root.open && !root.closePending
      repeat: true
      interval: 500
      onTriggered: { if (root.open && !search.activeFocus) search.forceActiveFocus() }
    }

    Rectangle {
      id: tabsPill
      anchors.horizontalCenter: card.horizontalCenter
      anchors.bottom: card.top
      anchors.bottomMargin: 8
      width: 240
      height: 28
      color: Theme.bg
      border.color: Theme.outline
      border.width: 1
      scale: 0.92 + 0.08 * card.cardProg
      opacity: card.cardProg

      RowLayout {
        anchors.fill: parent
        spacing: 0
        Repeater {
          model: [
            { label: "Text", idx: 0 },
            { label: "Images", idx: 1 }
          ]
          delegate: Item {
            required property var modelData
            Layout.fillWidth: true
            Layout.fillHeight: true
            property bool active: root.tab === (modelData.idx === 0 ? "text" : "images")
            Text {
              anchors.centerIn: parent
              text: modelData.label
              color: parent.active ? Theme.fg : Theme.muted2
              font.family: root.fontFamily
              font.pixelSize: 11
            }
            MouseArea {
              anchors.fill: parent
              cursorShape: Qt.PointingHandCursor
              onClicked: root.tab = modelData.idx === 0 ? "text" : "images"
            }
          }
        }
      }
    }

    Rectangle {
      id: card
      anchors.centerIn: parent
      width: root.boxWidth
      height: col.implicitHeight + 16
      color: Theme.bg
      border.color: Theme.outline
      border.width: 1
      property real cardProg: root.open ? 1 : 0
      scale: 0.92 + 0.08 * cardProg
      opacity: cardProg
      Behavior on cardProg { NumberAnimation { duration: 150; easing.type: Easing.OutExpo } }

      ColumnLayout {
        id: col
        anchors.left: parent.left
        anchors.right: parent.right
        anchors.top: parent.top
        anchors.leftMargin: 8
        anchors.rightMargin: 8
        anchors.topMargin: 8
        anchors.bottomMargin: 8
        spacing: 8

        Rectangle {
          Layout.fillWidth: true
          Layout.preferredHeight: 28
          color: "transparent"
          border.color: Theme.outline
          border.width: 1
          RowLayout {
            anchors.fill: parent
            anchors.leftMargin: 6
            anchors.rightMargin: 6
            spacing: 6
            Item {
              Layout.preferredWidth: 16
              Layout.preferredHeight: 16
              Layout.alignment: Qt.AlignVCenter
              QIcon {
                anchors.centerIn: parent
                name: "search"
                size: 16
                color: Theme.fg
              }
            }
            Item {
              Layout.fillWidth: true
              Layout.fillHeight: true
              Text {
                anchors.fill: parent
                verticalAlignment: Text.AlignVCenter
                text: "Search"
                color: Theme.muted2
                font.family: root.fontFamily
                font.pixelSize: 12
                visible: search.text === ""
                elide: Text.ElideRight
              }
              TextInput {
                id: search
                anchors.fill: parent
                verticalAlignment: TextInput.AlignVCenter
                font.family: root.fontFamily
                font.pixelSize: 12
                color: Theme.fg
                clip: true
                focus: true
                onTextChanged: root.query = text
                onVisibleChanged: {
                  if (visible) {
                    text = ""
                    forceActiveFocus()
                  }
                }
                Keys.onEscapePressed: event => { root.requestClose(); event.accepted = true }
                Keys.onTabPressed: { root.switchTab(); event.accepted = true }
                Keys.onBacktabPressed: { root.switchTab(); event.accepted = true }
                Keys.onUpPressed: { root.mouseOverList = false; root.selIdx = Math.max(0, root.selIdx - (root.tab === "images" ? 3 : 1)) }
                Keys.onDownPressed: { root.mouseOverList = false; root.selIdx = Math.min(root.currentList.length - 1, root.selIdx + (root.tab === "images" ? 3 : 1)) }
                Keys.onLeftPressed: if (root.tab === "images") { root.mouseOverList = false; root.selIdx = Math.max(0, root.selIdx - 1); event.accepted = true }
                Keys.onRightPressed: if (root.tab === "images") { root.mouseOverList = false; root.selIdx = Math.min(root.currentList.length - 1, root.selIdx + 1); event.accepted = true }
                Keys.onReturnPressed: root.paste(root.selIdx)
                Keys.onEnterPressed: root.paste(root.selIdx)
                Keys.onDeletePressed: root.remove(root.selIdx)
                Keys.onPressed: event => {
                  if ((event.key === Qt.Key_N || event.key === Qt.Key_P) && (event.modifiers & Qt.ControlModifier)) {
                    root.mouseOverList = false; root.selIdx = event.key === Qt.Key_N ? Math.min(root.currentList.length - 1, root.selIdx + 1) : Math.max(0, root.selIdx - 1)
                    event.accepted = true
                  } else if ((event.key === Qt.Key_D) && (event.modifiers & Qt.ControlModifier)) {
                    root.remove(root.selIdx)
                    event.accepted = true
                  } else if (event.key === Qt.Key_PageDown) {
                    root.mouseOverList = false
                    if (root.tab === "images")
                      root.selIdx = Math.min(root.currentList.length - 1, root.selIdx + root.pageStep())
                    else
                      root.selIdx = Math.min(root.currentList.length - 1, root.selIdx + 5)
                    event.accepted = true
                  } else if (event.key === Qt.Key_PageUp) {
                    root.mouseOverList = false
                    if (root.tab === "images")
                      root.selIdx = Math.max(0, root.selIdx - root.pageStep())
                    else
                      root.selIdx = Math.max(0, root.selIdx - 5)
                    event.accepted = true
                  }
                }
              }
            }
          }
        }

        Item {
          id: swipeContainer
          Layout.fillWidth: true
          Layout.preferredHeight: {
            const n = (root.currentList || []).length
            if (n === 0) return 0
            if (root.tab === "images") return Math.min(Math.ceil(n / 3) * root.imgCellH, 15 * 28)
            return Math.min(n * 28, 15 * 28)
          }
          visible: (root.currentList || []).length > 0
          Behavior on Layout.preferredHeight { NumberAnimation { duration: 70; easing.type: Theme.easingOut } }
          clip: true

          Row {
            id: swipeRow
            x: -root.swipeIdx * swipeContainer.width
            Behavior on x { enabled: root.open && !root.closePending; NumberAnimation { duration: 130; easing.type: Theme.easingOut } }
            height: swipeContainer.height

            Item {
              width: swipeContainer.width
              height: swipeContainer.height

              ListView {
                id: list
                anchors.fill: parent
                clip: true
                interactive: true
                flickableDirection: Flickable.VerticalFlick
                spacing: 0
                model: root.textEntries
                currentIndex: root.selIdx
                highlight: Rectangle { color: Theme.fg }
                highlightMoveDuration: 120

                delegate: Rectangle {
                  required property var modelData
                  required property int index
                  width: list.width
                  height: 28
                  readonly property bool isKeyboardSelected: root.selIdx === index
                  readonly property bool isHovered: root.hoverIdx === index
                  color: isHovered ? Theme.hover : "transparent"

                  Text {
                    anchors.fill: parent
                    anchors.leftMargin: 6
                    anchors.rightMargin: 6
                    verticalAlignment: Text.AlignVCenter
                    textFormat: Text.RichText
                    text: root.hl(modelData)
                    color: isKeyboardSelected ? Theme.onSelect : Theme.fg
                    font.family: root.fontFamily
                    font.pointSize: 12
                    elide: Text.ElideRight
                    clip: true
                  }

                  MouseArea {
                    anchors.fill: parent
                    hoverEnabled: true
                    cursorShape: Qt.PointingHandCursor
                    onEntered: { root.hoverIdx = index; root.mouseOverList = true }
                    onExited: { if (root.hoverIdx === index) root.hoverIdx = -1; root.mouseOverList = false }
                    onClicked: root.paste(index)
                  }
                }
              }
            }

            Item {
              width: swipeContainer.width
              height: swipeContainer.height

              GridView {
                id: imgGrid
                anchors.fill: parent
                clip: true
                interactive: true
                flickableDirection: Flickable.VerticalFlick
                cellWidth: Math.floor(imgGrid.width / 3)
                cellHeight: root.imgCellH
                model: root.imageEntries
                currentIndex: root.selIdx
                highlight: Rectangle { color: "transparent"; border.color: Theme.outline; border.width: 2 }
                highlightMoveDuration: 120

                delegate: Rectangle {
                  required property var modelData
                  required property int index
                  width: imgGrid.cellWidth
                  height: imgGrid.cellHeight
                  readonly property bool isSel: root.selIdx === index
                  readonly property bool isHover: root.hoverIdx === index
                  color: "transparent"

                  Image {
                    anchors.fill: parent
                    anchors.margins: 4
                    readonly property string _src: root.thumbs[root.idOf(modelData)] !== undefined ? "file://" + root.thumbDir + "/" + root.idOf(modelData) + ".png" : ""
                    source: _src
                    sourceSize.width: imgGrid.cellWidth - 8
                    sourceSize.height: imgGrid.cellHeight - 8
                    fillMode: Image.PreserveAspectCrop
                    asynchronous: true
                    cache: true
                    smooth: true
                  }

                  Rectangle {
                    anchors.fill: parent
                    anchors.margins: 4
                    color: isHover ? Theme.hover : "transparent"
                    border.width: 0
                    border.color: Theme.outline
                  }

                  MouseArea {
                    anchors.fill: parent
                    hoverEnabled: true
                    cursorShape: Qt.PointingHandCursor
                    onEntered: { root.hoverIdx = index }
                    onExited: { if (root.hoverIdx === index) root.hoverIdx = -1; root.mouseOverList = false }
                    onClicked: root.paste(index)
                  }
                }
              }
            }
          }
        }

      }
    }
  }
}
